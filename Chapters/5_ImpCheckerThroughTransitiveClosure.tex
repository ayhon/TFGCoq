\chapter{Implication checker from transitive closure}
\label{cap:imp-checker}

Having defined what an implication checker is, we are now left with the task of
developing the most advanced implication checker we can muster. Originally I
decided to approach this with the idea of modelling constraints as a graph, where
our \mintinline{coq}{cliteral}s are the nodes of the graph and the constraints its edges. With 
this representation we would transform the problem of testing whether an constraint is
implied into finding a path in a graph, for which many solutions are already 
known.

Unfortunately, this original approach had a problem, and it had to do with 
allowing our \mintinline{coq}{cliteral}s to include variables with an offset.
By allowing $x$ and $x + d$ for $d \in \mathbb{N}$ to be represented by 
different nodes, we no longer have a cheap way of expressing that if $x + d \ge y + d'$ then $x + d + k \ge y + d' + k$ for every $k\in \mathbb{Z}$. The graph
idea may be salvaged, but our definition of \mintinline{coq}{cliteral} could not be
its nodes.

While investigating the prior work we came across \cite{TransitiveClosure}, where
the author describes a $O(n^3)$ algorithm to compute the tightened transitive 
closure of a set of octagonal constraints.

\section{Tightened transitive closure of octagonal constraints}
\label{sect:tightened-transitive-closure}

Octagonal constraints are constraints of the form $\pm x - \pm y \le d$ or 
$\pm x \le d$ where $x,y,d \in \mathbb{Z}$. We say that constraints of the form
$\pm x - \pm y \le d$ are addition constraints and of the form $\pm x \le d$ are
bound constraints.

Our constraints can be translated to octagonal constraints through the following 
transformations. First, we exchange every \mintinline{coq}{C_EQ} constraint with two
\mintinline{coq}{C_LE} constraints and every \mintinline{coq}{LT} constraints with a 
\mintinline{coq}{LE} constraint by taking advantage of the fact that since these are 
integer constraints, if $x < y$ then $x \le y-1$. Afterwards, we get rid of all 
inequalities of the form $d \le d'$ where $d$ ad $d'$ are constant values and not
variables. After all, these inequalities do not reveal any information, since they
are always true if the constraints are satisfiable, and we assume our constraints
are indeed satisfiable. Finally, we transform all \mintinline{coq}{cliteral}s of the
\mintinline{coq}{C_VAR} constructor to a \mintinline{coq}{C_VAR_DELTA} constructor, where
the \mintinline{coq}{delta} field is left as 0. From there, if our constraints are not
already in the form of an octagonal constraint, it is enough to multiple both sides
by $-1$ or move around terms from one side of the inequality to the other until 
they do.

Given an octagonal addition constraint of the form $ax + by\le d$ where $a,b\in
\{-1,1\}$, we say that $ax + by \le d$ \emph{trivially implies} $ax + by \le d'$
if $d \le d'$. We say that a set of constraints $C'$ is the transitive closure of
$C$ if every constraint implied by $C$ is trivially implied by a constraint in 
$C'$.

Therefore, if we had an algorithm to compute the transitive closure of a set of
constraints $C$ we could implement a conjunction implication checker testing
whether the thesis is trivially implied by any of the constraints in the 
transitive closure of the hypothesis. This algorithm is our final goal.

In \cite{TransitiveClosure} another simpler algorithm is mentioned to compute the
transitive closure of a set of octagonal constraints, first described in 
\cite{HarveyStuckey}. This algorithm relies on iteratively adding to an already 
transitively close set new additive constraints, along with all the constraints needed
to keep the set transitively closed.

In particular, if $A$ is a set of addition constraints and $B$ is a set of bound 
constraints, where $A \cup B$ is transitively closed, then 
${A \cup B \cup \{ax+by \le d\} \cup A' \cup B'}$ is also transitively closed, where
$A'$ is formed by the addition constraints
\begin{enumerate}[label={\small \boxed{\textbf{A{\arabic*}}}}]
    \item\label{rule:A1} $ez + by \le d + d'$ if $-ax + ez \le d'$, $z \ne y$
    \item\label{rule:A2} $ax + ft \le d + d'$ if $ft - by \le d'$, $t \ne x$
    \item\label{rule:A3} $ex + ft \le d + d' + d''$ if $-ax + ex \le d'$, $-by + ft \le d''$ and $t$, $z$, $x$ and $yz$ are all different.
\end{enumerate}

\noindent and $B'$ is formed by the bound constraints

\begin{enumerate}[label={\small \boxed{\textbf{B{\arabic*}}}}]
    \item\label{rule:B1} $by \le d + d'$ if $-ax \le d'$
    \item\label{rule:B2} $ax \le d + d'$ if $-by \le d'$
    \item\label{rule:B3} $ez \le d + d' + d''$ if $-ax \le d'$ and $-by + ex \le d''$ with $z \ne x$
    \item\label{rule:B4} $ft \le d + d' + d''$ if $-by \le d'$ and $-ax + ft \le d''$ with $t \ne y$
    \item\label{rule:B5} $by \le \lfloor \frac{d + d'}{2}\rfloor$ if $-ax + by \le d'$
    \item\label{rule:B6} $ax \le \lfloor \frac{d + d'}{2}\rfloor$ if $-by + ax \le d'$
    \item\label{rule:B7} $ez \le \lfloor \frac{d + d' + d''}{2}\rfloor$ if $-ax + ez \le d'$ and $-by + ez \le d''$
\end{enumerate}

With this result one can derive an algorithm to obtain the transitive closure of any
set of octogonal constraints. Given $C_A$ and $C_B$ sets of addition and bound 
constraints respectively, we start with $B = C_B$ and 
$A = \{ax + by \le d + d'\ :\  \forall ax\le d\in C_B, by\le d'\in C_B\}$ and 
iteratively add the elements in $C_A$ using the rules above, taking care to remove
any constraint which may be trivially implied by another.

Based on these rules we implemented a simpler algorithm which is just as powerful but
with the downside of requiring more iterations to derive the transitive closure.
The reason is that these rules are useful if we want to prove the completeness of the 
transitive closure, but since we are only interested in the soundness of the algorithm,
we can notice that some of these rules are actually the result of applying one or more
of the other rules.

In particular, we remove the rules:
\begin{itemize}
    \item  \ref{rule:A3} since it can be derived from successive uses of \ref{rule:A1}
           or \ref{rule:A2}.
    \item  \ref{rule:B3} since it can be derived from successive uses of \ref{rule:B1}
           and \ref{rule:B2}.
           $$
           \begin{array}{llll}
           -ax \le d'&\quad \phantom{-}ax + by \le d &\overset{\ref{rule:B1}}{\Longrightarrow} &by \le d + d' \\
           \\
           \phantom{-}bx \le d + d'&\quad -by + ex \le d'' &\overset{\ref{rule:B2}}{\Longrightarrow} &ez \le d + d' + d'' \\
           \end{array}
           $$
    \item  \ref{rule:B4} since it's the symmetric of \ref{rule:B3}.
    \item  \ref{rule:B7} since it can be obtained by successively applying \ref{rule:A1} and
           \ref{rule:B6} or \ref{rule:A2} and \ref{rule:B5}.
           $$
           \begin{array}{llll}
           -ax + ex \le d' & \phantom{-}ax + by \le d &\overset{\ref{rule:A1}}{\Longrightarrow} &
             ex + by \le d + d' \\
           \phantom{-}ex + by \le d + d' & -by + ex \le d'' &\overset{\ref{rule:B6}}{\Longrightarrow} &
             ex \le \lfloor\frac{d + d' + d''}{2}\rfloor \\
           \end{array}
           $$
\end{itemize}

If we eliminate the requirement that after each new constraint added the set must be 
transitively closed, we can actually go without these rules. After adding a new 
constraint we would just have to apply the rules also with the constraint we derived at
that step until no new constraints can be added. And since we no longer care if the
intermediate set of constraints is transitively closed or not, we can drop the 
requirement of adding the constraints iteratively and simply, for our currently built
set of constraints, test for every pair whether they can be combined and add the 
result if its not trivially implied by any of the other constraints already in the 
set.

Therefore, the final rules we have are:
\begin{enumerate}[label={\small \boxed{\textbf{R{\arabic*}}}}]
    \item\label{rule:R1}
    $ax + by \le d \quad -by + ez \le d' \quad\Longrightarrow ax + ez \le d + d'$
    \item\label{rule:R2}
    $ax + by \le d \quad -ax + ez \le d' \quad\Longrightarrow by + ez \le d + d'$
    \item\label{rule:R3}
    $ax + by \le d \quad \phantom{-}ez - ax  \le d' \quad\Longrightarrow by + ez \le d + d'$
    \item\label{rule:R4}
    $ax + by \le d \quad \phantom{-}ez - bx  \le d' \quad\Longrightarrow by + ez \le d + d'$
    \item\label{rule:R5}
    $ax + by \le d \quad -ax \le d' \quad\Longrightarrow by \le d + d'$
    \item\label{rule:R6}
    $ax + by \le d \quad -by \le d' \quad\Longrightarrow ax \le d + d'$
    \item\label{rule:R7}
    $ax \le d \quad by \le d' \quad\Longrightarrow ax + by \le d + d'$
\end{enumerate}

The algorithm, in pseudocode, would look like this
\begin{minted}{lua}
function TransitiveClosure(C: constraints) -> constraints
    repeat
        new_constraints := [
            combine(c1,c2) for c1 in C for c2 in C
        ]
        C = join(C, new_constraints)
    until C not changed
    return C
\end{minted}
\noindent We have to pay attention to not include unnecessary constraints when adding
new ones to the set, which is why a \mintinline{coq}{join} function is explicitly used.
Applying the rules is left for the \mintinline{coq}{combine} function.

\section{Implementation of the implication checker}

Since the representation of the constraints is not exactly the same for our new 
algorithm, we have opted to include these definitions in its own file, aptly named
\verb|octagon.v|. Implementing an implication checker as defined before in terms of
the algorithm developed in this chapter is left as future work\footnote{See Section 
\ref{sect:future-work} for more information.}. First of all, we need to define in Coq 
what are our octagonal constraints. Since there are two types of octagonal constraints,
additive and bound constraints, we represent this with two different constructors.

\begin{minted}{coq}
Inductive Constraint :=
 | AddConstr (l r: term)(d: Z)
 | BndConstr (t: term)(d: Z).
\end{minted}

In this definition, the terms represent the $\pm x$ elements in our constraints. To
represent the $\pm$ in Coq we use the subset $\{1,-1\} \subset \mathbb{Z}$.

\begin{minted}{coq}
Definition pmUnit := { z : Z | z = 1 \/ z = - 1 }.
Record term := {a: pmUnit; x: nat}.
\end{minted}

Variables in this representation are still identified with natural numbers, but since
they can now take any integer values we need to also change our previous \mintinline{coq}{assignment}
type definition, which we call now the type of \mintinline{coq}{model}s.

\begin{minted}{coq}
Definition model : Type := nat -> Z.
\end{minted}

Therefore, we say that a model \mintinline{coq}{m} satisfies a single constraint \mintinline{coq}{m}
if the following function returns true.

\begin{minted}{coq}
Definition satisfies_single_constraint 
    (m: model) (c: Constraint): bool  := 
  match c with 
  | AddConstr l r d => term_value m l + term_value m r <=? d
  | BndConstr t d => term_value m t <=? d
  end.
\end{minted}

\mintinline{coq}{term_value} is a function which retrieves the value of the variable 
\mintinline{coq}{x} according to the model \mintinline{coq}{m} and multiplies it by 
\mintinline{coq}{a} to get the value of the term under the model.

\begin{minted}{coq}
Definition term_value(m: model)(t: term): Z := 
    proj1_sig t.(a) * (m t.(x)).
\end{minted}

% Explain as a standalone theory
% Leave integration of the `octagon.v` file to `constraints.v` for "future work"

% Start with approaches that did not work -> Graph traversal
% Explain WHY it does not work (constraints are invariant to translation)
% Introduce the papers which inspired us to follow this approach

In this new file we have also ported over a more general definition of implication of
constraints, which works over conjunctions. Since we showed in Chapter \ref{cap:definitions}
that conjunction implication checkers are equivalent to implication checkers, in this
file we drop the requirement of disjunctions in the constraints and simply treat our
hypothesis as conjunctions of constraints.

We also include a more general version of implication which works with a conjunction
of thesis constraints, and add some notation to help us work with it more comfortably.

\begin{minted}{coq}
Definition implication(C: list Constraint)(C': list Constraint) :=
  forall m, 
    satisfies_constraints m C = true ->
    satisfies_constraints m C' = true.
Infix "==>>" := implication (at level 96, right associativity).
\end{minted}

In this definition, \mintinline{coq}{satisfies_constraints} is implemented as a 
\mintinline{coq}{forallb} over the \mintinline{coq}{satisfies_single_constraint} 
function. Its definition reads as ``for every model which satisfies $C$, it also
satisfies $C'$''.

The usual facts about implication are also true for \mintinline{coq}{==>>}, most notably.

\begin{itemize}
    \item Reflexivity
\begin{minted}{coq}
Theorem implication_refl(C: list Constraint):
  (C ==>> C).
\end{minted}

    \item Transitivity
\begin{minted}{coq}
Theorem implication_trans(C C' C'': list Constraint):
  (C ==>> C') -> (C' ==>> C'') -> (C ==>> C'').
\end{minted}
  
\end{itemize}

With these new definitions we proceed to define the functions which will help us 
compute the transitive closure, or at least given a list of constraints $C$ help bring
it closer to its transitive closure.

Since our algorithm consists of performing successively a given number of iterations
over the list we define an \mintinline{coq}{iterate} which we will call consecutively to
iteratively bring closer our constraints $C$ to its transitive closure.

\begin{minted}{coq}
Definition iterate(C: list Constraint) : list Constraint :=
  let C' := new_constraints C in
  let C'' := flatten C' in
  join C C''.
\end{minted}

If we de-sugar the definition, we will see that \mintinline{coq}{iterate} is in fact the composition of
three different functions: 

\begin{itemize}
    \item \mintinline{coq}{new_constraints}
    \item \mintinline{coq}{flatten}
    \item \mintinline{coq}{join C}
\end{itemize}

\mintinline{coq}{new_constraints} itself is just a function which takes every pair of constraints in 
\mintinline{coq}{C} and collects all the constraints obtained from their combination.
These combinations come from the rules in Harvey's and Stuckey's algorithm\cite{HarveyStuckey},
but simplified to make our life easier. The rules themselves are implemented in the 
\mintinline{coq}{combine} function.

\begin{minted}{coq}
Definition new_constraints(C: list Constraint): list Constraint :=
  flat_map (fun c => 
    flat_map (fun c' => 
        opt_to_list(combine c c')
    ) C
  ) C.
  
Definition combine(c c': Constraint): option Constraint :=
  match c, c' with
  | AddConstr l r d, AddConstr l' r' d' =>
      if      l =? (op l') then Some (AddConstr r r' (d + d'))
      else if l =? (op r') then Some (AddConstr r l' (d + d'))
      else if r =? (op l') then Some (AddConstr l r' (d + d'))
      else if r =? (op r') then Some (AddConstr l l' (d + d'))
      else None
  | AddConstr l r d, BndConstr t' d' =>
      if      l =? (op t') then Some (BndConstr r (d + d'))
      else if r =? (op t') then Some (BndConstr l (d + d'))
      else None
  | BndConstr t d, AddConstr l' r' d' =>
      if      l' =? (op t) then Some (BndConstr r' (d + d'))
      else if r' =? (op t) then Some (BndConstr l' (d + d'))
      else None
  | BndConstr t d, BndConstr t' d' => Some (AddConstr t t' (d + d'))
  end.
\end{minted}

The \mintinline{coq}{flatten} function is just a map over the function 
\mintinline{coq}{normalize_constraints}, which performs a very simple transformation, those of 
the constraints of the form $x + x \le d$, which turn into $x \le \lfloor \frac{d}{2}\rfloor$.

\begin{minted}{coq}
Definition normalize_constraint(c: Constraint): Constraint :=
   match c with
   | AddConstr l r d => 
       if l =? r 
       then BndConstr l (d / 2)
       else c
   | c => c
   end.
\end{minted}

In fact, this is an important transformation, referred to as \emph{tightening} in the
literature \cite{TransitiveClosure}. Since we know that our variables hold integer
values, if we have a constraint of the form $x + y \le d + \epsilon$ where $\epsilon$
is a real number in the interval $(0,1)$ and $x,y,d\in \mathbb{Z}$ then we know that 
$x + y \le d$, since that is its closest integer value to $d+\epsilon$. Sometimes this
can allow us to improve upon our original constraints. Consider, for example, that we 
had the following constraints.

\begin{align}
    \label{C1} x + y &\le 10  \\
    \label{C2} x - y &\le 3 \\
    \label{C3} y \le 3
\end{align}

Then, combining \ref{C1} and \ref{C2} we can deduce that $x + x \le 13$, which we can
tighten into the bound constraint $x \le 6$. Then, combining this with \ref{C3} we 
obtain the addition constraint $x + y \le 9$, which is an improvement over \ref{C1}.

Finally the \mintinline{coq}{join} is potentially one of the most complicated functions
in this file. Its purpose is to merge two lists of constraints into one, but getting 
rid of those constraints which are duplicated or no longer needed.

\begin{minted}{coq}
Definition joined(c': Constraint)(cs: list Constraint) :=
  c' :: filter (fun c => negb (trivial_impl c' c)) cs.

Definition join(C C': list Constraint): list Constraint :=
  fold_left (fun cs c' => 
    if forallb (fun c => negb (trivial_impl c c')) cs
    then joined c' cs
    else cs
  ) C' C.
\end{minted}

The property we want to prove for \mintinline{coq}{iterate} is the following.

\begin{minted}{coq}
Theorem iterate_implication(C T: list Constraint):
  (C ==>> iterate C).
\end{minted}

This is but a corollary of the following theorems.
\begin{minted}{coq}
Theorem flatten_implication(C T: list Constraint):
  (C ==>> T) -> (C ==>> flatten T).
Theorem join_implication(C T: list Constraint):
  (C ==>> T) -> (C ==>> join C T).
Theorem new_constraints_implication(C T: list Constraint):
  (C ==>> T) -> (C ==>> new_constraints T).
\end{minted}

Once these are proven, the proof of \mintinline{coq}{iterate_implication} is as simple as performing
successive applications of these theorems.
\begin{minted}{coq}
Theorem iterate_implication(C: list Constraint):
  (C ==>> iterate C).
Proof.
  apply join_implication.
  apply flatten_implication.
  apply new_constraints_implication.
  apply implication_refl.
Qed.
\end{minted}

The proofs for these theorems can be found in the file \verb|octagon.v| in \verb|FORVES2|'s GitHub
repository.

With this \mintinline{coq}{iterate} function defined and the \mintinline{coq}{iterate_implication}
theorem proven, the only piece left to construct our implication checker is a function which would
allow us to apply the \mintinline{coq}{iterate} function over the original constraints a given
number of times.

\begin{minted}{coq}
Fixpoint church_numeral{A: Type}(n: nat)(f: A -> A)(x: A):=
  match n with
  | O => x
  | S m => f (church_numeral m f x)
  end.
\end{minted}

We call this function \mintinline{coq}{church_numeral} since it actually implements a translation
between Peano-encoded naturals (the \mintinline{coq}{nat} type in Coq) and Church-encoded naturals,
where the number \mintinline{coq}{n} corresponds to the function which takes another function 
\mintinline{coq}{f} and a value \mintinline{coq}{x} and applies the function over the value 
\mintinline{coq}{n} times.

With these definitions it is now trivial to construct an implication checker. The only decision
left to make is how many times we want to \mintinline{coq}{iterate}. A good choice ends up
being twice the number of our hypothesis constraints. This is because when deriving the rules of
our algorithm (implemented in \mintinline{coq}{combine}) we took care in ensuring that any rule
from Harvey's and Stuckey's algorithm\cite{HarveyStuckey} could be derived by applying two of 
our own, and their algorithm is proven to be complete. Even if this completeness is not proven
in Coq, it ends up being the optimal choice.

\begin{minted}{coq}
Program Definition conj_trans_closure_checker
    (n: nat) : conj_imp_checker := {|
  conj_imp_checker_fun cs c := 
    let trans_closure := church_numeral n iterate cs in 
    existsb (fun c' => trivial_impl c' c) trans_closure
|}.
Next Obligation. 
(* The proof proceeds by induction. We informally
   prove it for the case n=1, assuming n=0 as proven.
   Let  c' be a constraint in the transitive closure
   such that [c'] ==>> [c]. Then, we have:
            cs ==>> iterate cs    from iterate_implication
    iterate cs ==>> [c']          from imply_refl
          [c'] ==>> [c]           from our hypothesis
   Through transitivity, we conclude that cs ==>> [c]. *)
Qed. 
Program Definition trans_closure_checker(n: nat) : imp_checker 
    := mk_imp_checker (conj_trans_closure_checker n).
\end{minted}

Take note that the \mintinline{coq}{mk_imp_checker} used in this module is not
the one we defined in Chapter \ref{cap:definitions}, since we are using a different definition
to represent the state and constraints. Most notably, in this section we have used
integers while we previously restricted ourselves to natural numbers. An explanation
on how to bridge this implementation with the definitions in Chapter \ref{cap:definitions}
will be described in Section \ref{sect:future-work}.